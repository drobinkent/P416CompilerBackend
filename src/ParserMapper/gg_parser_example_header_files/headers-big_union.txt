#
# Big-Union parse graph (see paper)
#

ethernet {
	fields {
		dstAddr : 48 : extract,
		srcAddr : 48 : extract,
		etherType : 16 : extract,
	}
	next_header = map(etherType) {
		0x8100, 0x9100, 0x9200, 0x9300 : ieee802-1q,
		0x88a8 : ieee802-1ad,
		0x8847, 0x8848 : mpls,
		0x0800 : ipv4,
		0x86dd : ipv6,
		0x0806, 0x8035 : arp_rarp,
	}
	# This is some test code
}

ethernet2 {
	fields {
		dstAddr : 48 : extract,
		srcAddr : 48 : extract,
		etherType : 16 : extract,
	}
}

ieee802-1q {
	fields {
		pcp : 3 : extract,
		cfi : 1,
		vid : 12 : extract,
		etherType : 16 : extract,
	}
	next_header = map(etherType) {
		0x8100, 0x9100, 0x9200, 0x9300 : ieee802-1q,
		0x8847, 0x8848 : mpls,
		0x0800 : ipv4,
		0x86dd : ipv6,
		0x0806, 0x8035 : arp_rarp,
	}
	max_var = vlan
	max = 2
}

ieee802-1ad {
	fields {
		pcp : 3 : extract,
		cfi : 1,
		vid : 12 : extract,
		etherType : 16 : extract,
	}
	next_header = map(etherType) {
		0x8100, 0x9100, 0x9200, 0x9300 : ieee802-1q,
		0x88e7 : ieee802-1ah,
	}
	max_var = vlan
	max = 2
}

ieee802-1ah {
	fields {
		pcp : 3 : extract,
		dei : 1,
		uca : 1,
		reserved : 3,
		i-sid : 24 : extract,
	}
	next_header = ethernet2
}

mpls {
	fields {
		label : 20 : extract,
		tc : 3 : extract,
		bos : 1 : extract,
		ttl : 8 : extract,
	}
	pseudo-fields {
		next-header : 4,
	}
	next_header = map(bos, next-header) {
		b0xxxx : mpls,
		b10000 : eompls,
		b10100 : ipv4,
		b10110 : ipv6,
	}
	next_header_def = b10001
	max = 5
	length = 32
}

ipv4 {
	fields {
		version : 4,
		ihl : 4,
		diffserv : 8 : extract,
		totalLen : 16,
		identification : 16,
		flags : 3 : extract,
		fragOffset : 13,
		ttl : 8 : extract,
		protocol : 8 : extract,
		hdrChecksum : 16,
		srcAddr : 32 : extract,
		dstAddr : 32 : extract,
		options : *,
	}
	next_header = map(fragOffset, protocol) {
		1 : icmp,
		6 : tcp,
		17 : udp,
		47 : gre,
		50 : ipsec_esp,
		51 : ipsec_ah,
		132 : sctp,
	}
	length = ihl * 4 * 8
	max_length = 256
}

ipv6 {
	fields {
		version : 4,
		trafficClass : 8 : extract,
		flowLabel : 20 : extract,
		payloadLen : 16,
		nextHdr : 8 : extract,
		hopLimit : 8,
		srcAddr : 128 : extract,
		dstAddr : 128 : extract,
	}
	next_header = map(nextHdr) {
		58 : icmpv6,
		6 : tcp,
		17 : udp,
		47 : gre,
		50 : ipsec_esp,
		51 : ipsec_ah,
		132 : sctp,
	}
}

icmp {
	fields {
		type : 8 : extract,
		code : 8 : extract,
		hdrChecksum : 16,
	}
}

icmpv6 {
	fields {
		type : 8 : extract,
		code : 8 : extract,
		hdrChecksum : 16,
	}
}

tcp {
	fields {
		srcPort : 16 : extract,
		dstPort : 16 : extract,
		seqNo : 32,
		ackNo : 32,
		dataOffset : 4,
		res : 3,
		ecn : 3,
		ctrl : 6,
		window : 16,
		checksum : 16,
		urgentPtr : 16,
		options : *,
	}
	length = dataOffset * 4 * 8
	max_length = 192
}

udp {
	fields {
		srcPort : 16 : extract,
		dstPort : 16 : extract,
		length : 16,
		checksum : 16,
	}
	next_header = map(dstPort) {
		65535 : vxlan, # Made up value for port -- not yet assigned
	}
}

sctp {
	fields {
		srcPort : 16 : extract,
		dstPort : 16 : extract,
		verifTag : 32,
		checksum : 32,
	}
}

gre {
	fields {
		C : 1,
		R : 1,
		K : 1,
		S : 1,
		s : 1,
		recurse : 3,
		flags : 5,
		ver : 3,
		proto : 16 : extract,
	}
	next_header = map(K, proto) {
		0x16558 : nv_gre_inner,
		0x16559 : gre,
	}
	max = 3
}

nv_gre_inner {
	fields {
		tni : 24 : extract,
		reserved : 8
	}
	next_header = ethernet2
}

ipsec_esp {
	fields {
		spi : 32,
		seqNo : 32,
	}
}

ipsec_ah {
	fields {
		nextHdr : 8 : extract,
		length : 8,
		zero : 16,
		spi : 32,
		seqNo : 32,
	}
	next_header = map(nextHdr) {
		1 : icmp,
		6 : tcp,
		17 : udp,
		47 : gre,
		58 : icmpv6,
		132 : sctp,
	}
}

arp_rarp {
	fields {
		hwType : 16,
		protoType : 16,
		hwAddrLen : 8,
		protoAddrLen : 8,
		opcode : 16 : extract,
	}
	next_header = map(protoType) {
		0x0800 : arp_rarp_ipv4
	}
}

arp_rarp_ipv4 {
	fields {
		srcHwAddr : 48 : extract,
		srcProtoAddr : 32 : extract,
		dstHwAddr : 48 : extract,
		dstProtoAddr : 32 : extract,
	}
}

eompls {
	fields {
		zero : 4,
		reserved : 12,
		seqNo : 16,
	}
	next_header = ethernet2
}

vxlan {
	fields {
		flags : 8,
		reserved : 24,
		vni : 24 : extract,
		reserved2 : 8,
	}
	next_header = ethernet2
}
